{
  config,
  lib,
  pkgs,
  ...
}: let
  # automatically import ssh hosts as rclone remotes
  ssh_hosts =
    lib.mapAttrs
    (name: block: {
      config = {
        type = "sftp";
        host = block.data.hostname;
        user = block.data.user;
        "global.links" = true;
        "global.vfs_cache_mode" = "writes";
      };
      secrets = {};
      mounts =
        if (lib.strings.hasPrefix "qrc" name)
        then {
          "./" = {
            enable = true;
            mountPoint = "${config.home.homeDirectory}/Resources/${name}";
          };
        }
        else {};
    })
    # first drop the global section
    (lib.filterAttrs (n: _: n != "*") config.programs.ssh.matchBlocks);
in {
  programs.rclone = {
    enable = true;
    remotes = ssh_hosts;
  };

  # The home-manager module for rclone relies on systemd both to generate the
  # configuration file and to create the mount points.systemd.
  # Since not available on MacOS (and no automated translation to launchd is easily
  # available) the easiest alternative is to manually generate the configuration file.
  xdg.configFile."rclone/_rclone.conf" = {
    enable = pkgs.stdenv.isDarwin;
    text =
      lib.mkIf pkgs.stdenv.isDarwin
      (lib.generators.toINIWithGlobalSection {} {
        globalSection = {};
        sections =
          {}
          # add ssh hosts, providing public keys
          // (lib.mapAttrs (_: cfg:
            cfg.config
            // {
              key_file = "${config.home.homeDirectory}/.ssh/id_ed25519";
            })
          ssh_hosts);
      });
  };

  # Rclone requires the folder of the configuration file to be writable, since it will
  # be used at run time to store some other temporary configurations.
  # However, the file generated by home manager will be a link, and, once
  # de-referenced, its parent directory will be in the nix store, and thus not
  # writable.
  # The present workaround is to just copy the generated configurations to a regular
  # file, circumventing the linking process.
  home.activation.copyRcloneConfig =
    lib.mkIf pkgs.stdenv.isDarwin
    (lib.hm.dag.entryAfter ["writeBoundary" "linkGeneration"] ''
      run cp \
        ${config.xdg.configHome}/rclone/_rclone.conf \
        ${config.xdg.configHome}/rclone/rclone.conf
      run chmod +w ${config.xdg.configHome}/rclone/rclone.conf
    '');
}
