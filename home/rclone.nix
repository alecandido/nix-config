{
  config,
  lib,
  pkgs,
  ...
}: {
  programs.rclone = {
    enable = true;
    remotes = {
      qrc = {
        config = {
          type = "sftp";
          host = "qrc";
          user = "alessandro.candido";
        };
        secrets = {};
        mounts = {};
      };
    };
  };

  # The home-manager module for rclone relies on systemd both to generate the
  # configuration file and to create the mount points.systemd.
  # Since not available on MacOS (and no automated translation to launchd is easily
  # available) the easiest alternative is to manually generate the configuration file.
  xdg.configFile."rclone/_rclone.conf".text =
    lib.mkIf pkgs.stdenv.isDarwin
    (lib.generators.toINIWithGlobalSection {} {
      globalSection = {};
      sections = {
        qrc = {
          type = "sftp";
          host = "login.qrccluster.com";
          user = "alessandro.candido";
          key_file = "${config.home.homeDirectory}/.ssh/id_ed25519";
        };
      };
    });

  # Rclone requires the folder of the configuration file to be writable, since it will
  # be used at run time to store some other temporary configurations.
  # However, the file generated by home manager will be a link, and, once
  # de-referenced, its parent directory will be in the nix store, and thus not
  # writable.
  # The present workaround is to just copy the generated configurations to a regular
  # file, circumventing the linking process.
  home.activation.copyRcloneConfig =
    lib.mkIf pkgs.stdenv.isDarwin
    (lib.hm.dag.entryAfter ["writeBoundary" "linkGeneration"] ''
      run cp \
        ${config.xdg.configHome}/rclone/_rclone.conf \
        ${config.xdg.configHome}/rclone/rclone.conf
      run chmod +w ${config.xdg.configHome}/rclone/rclone.conf
    '');
}
